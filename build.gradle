apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'pmd'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = 'application.service.App'
group = 'application.service'
artifactId = 'application-service'
version = currentVersion

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.10'
    }
}

project.ext {
    dropwizardVersion = '1.0.0-rc4'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
        'io.dropwizard:dropwizard-core:' + dropwizardVersion,
        'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion,
        'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
        'postgresql:postgresql:9.1-901-1.jdbc4'
    )
    testCompile (
        'io.dropwizard:dropwizard-testing:' + dropwizardVersion,
        'com.h2database:h2:1.4.181'
    )
}

pmd {
    ignoreFailures = false
    consoleOutput = true
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/pmd")
}

test {
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

run {
    args 'server', './config.yml'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

shadowJar {
   baseName = 'application-service'
   classifier = 'fat'
}

artifactory {
    contextUrl = artifactoryUrl
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = artifactoryUser
            password = artifactoryPassword
        }
        defaults {
            publications ('mavenJava')
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId group
            artifactId artifactId
            version version
            from components.java
            artifact shadowJar
        }
    }
}
